{
  parserClass="me.hardcoded.arucas.language.ArucasParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Arucas"
  psiImplClassSuffix="Impl"
  psiPackage="me.hardcoded.arucas.psi"
  psiImplPackage="me.hardcoded.arucas.psi.impl"

  elementTypeHolderClass="me.hardcoded.arucas.psi.ArucasTypes"
  elementTypeClass="me.hardcoded.arucas.psi.ArucasElementType"
  tokenTypeClass="me.hardcoded.arucas.psi.ArucasTokenType"

  tokens = [
    COMMENT='regexp://.*[^\n\r\f]|/\*(.|[\r\n\f])*?\*/'
    WHITESPACE='regexp:\s+'
    NUMBER='regexp:[0-9]+(\\.[0-9]+)?'
    KW_TRUE="true"
    KW_FALSE="false"
    KW_NULL="null"
    KW_THIS="this"
    KW_IF="if"
    KW_ELSE="else"
    KW_FOR="for"
    KW_FOREACH="foreach"
    KW_WHILE="while"
    KW_TRY="try"
    KW_CATCH="catch"
    KW_FUN="fun"
    KW_CLASS="class"
    KW_OPERATOR="operator"
    KW_NEW="new"
    KW_STATIC="static"
    KW_VAR="var"
    KW_SWITCH="switch"
    KW_CASE="case"
    KW_DEFAULT="default"
    KW_RETURN="return"
    KW_BREAK="break"
    KW_CONTINUE="continue"
    KW_IMPORT="import"
    KW_FROM="from"
    KW_FINALLY="finally"
    KW_LOCAL="local"
    KW_ENUM="enum"
    KW_INTERFACE="interface"
    KW_SUPER="super"
    KW_THROW="throw"
    IDENTIFIER='regexp:[a-zA-Z_][a-zA-Z0-9_]*'
    STRING="regexp:('([^'\\]|\\.)*'|\"([^\"\\]|\\.)*\")|(\"([^\"\\]|\\.)*\"|'([^'\\]|\\.)*\")"
    OP_LOGICAL_OR="||"
    OP_LOGICAL_AND="&&"
    OP_EQUALS="=="
    OP_NOTEQUALS="!="
    OP_MORETHANEQ=">="
    OP_MORETHAN=">"
    OP_LESSTHANEQ="<="
    OP_LESSTHAN="<"
    OP_PLUS="+"
    OP_MINUS="-"
    OP_MUL="*"
    OP_DIV="/"
    OP_POW="^"
    OP_NOT="!"
    OP_MM="--"
    OP_PP="++"
    OP_BSHL="<<"
    OP_BSHR=">>"
    OP_BOR="|"
    OP_BAND="&"
    OP_BXOR="~"
    ARBITRARY="..."
    COMMA=","
    SEMICOLON=";"
    ASSIGNMENT="="
    POINTER="->"
    DOT="."
    COLON=":"
    LPAREN="("
    RPAREN=")"
    LBRACK="["
    RBRACK="]"
    LBRACE="{"
    RBRACE="}"
  ]
}

// ROOT
arucas ::= Statement *

CodeBlock ::= '{' ( '}' | Statement+ '}' )
Statement ::= CodeBlock
  | ';'
  | IfStatement
  | WhileStatement
  | ClassDeclaration
  | EnumDeclaration
  | InterfaceDeclaration
  | ImportStatement
  | ThrowStatement
  | FunctionStatement
  | TryStatement
  | ForEachStatement
  | ForStatement
  | SwitchStatement
  | ReturnStatement
  | LocalStatement
  | ContinueStatement
  | BreakStatement
  | ExpressionStatement

IdentifierName ::= IDENTIFIER {extends=Name}
ClassName ::= IDENTIFIER {extends=Name}
VariableName ::= IDENTIFIER {extends=Name}
FunctionName ::= IDENTIFIER {extends=Name}

fake Name ::= {
  mixin="me.hardcoded.arucas.psi.impl.ArucasNamedElementImpl"
  implements="me.hardcoded.arucas.psi.ArucasNamedElement"
  methods=[setName getNameIdentifier]
}

ImportStatement ::= 'import' ImportElement 'from' ImportPath ';' {extends=Statement}
ImportElement ::= '*'
  | ImportClassName ( ',' ImportClassName)*
ImportClassName ::= IDENTIFIER {extends=Name}
ImportPath ::= IDENTIFIER ( '.' IDENTIFIER )*

ThrowStatement ::= 'throw' Expression ';' {extends=Statement}
ReturnStatement ::= 'return' Expression? ';' {extends=Statement}
LocalStatement ::= 'local' IDENTIFIER NamedTypeHint? '=' Expression ';' {extends=Statement}
ContinueStatement ::= 'continue' ';' {extends=Statement}
BreakStatement ::= 'break' ';' {extends=Statement}
ExpressionStatement ::= Expression (&',' UnpackExtension)? ';' {extends=Statement pin=1}
UnpackExtension ::= (',' Expression)+

IfStatement ::= 'if' '(' Expression ')' Statement ElseStatement? {extends=Statement}
ElseStatement ::= 'else' Statement {extends=Statement}
WhileStatement ::= 'while' '(' Expression ')' Statement {extends=Statement}

/* Enum Statement */
EnumDeclaration ::= 'enum' ClassName ClassInheritance? EnumCodeBlock {extends=Statement}
EnumCodeBlock ::= '{' EnumCodeValues? (';' ClassBodyStatement*)? '}' {extends=Statement}
EnumCodeValues ::= EnumValue (',' EnumValue )* ','?
EnumValue ::= IDENTIFIER CallArguments?

/* Interface Statement */
InterfaceDeclaration ::= 'interface' ClassName InterfaceCodeBlock {extends=Statement}
InterfaceCodeBlock ::= '{' InterfaceBodyStatement* '}' {extends=Statement}
private InterfaceBodyStatement ::= InterfaceMethod {extends=Statement}
InterfaceMethod ::= 'fun' FunctionName Arguments NamedTypeHint? ';'

/* Class Statement */
ClassDeclaration ::= 'class' ClassName ClassInheritance? ClassCodeBlock {extends=Statement}
ClassInheritance ::= ':' ClassName (',' ClassName)*
ClassCodeBlock ::= '{' ClassBodyStatement* '}' {extends=Statement}
private ClassBodyStatement ::= ClassMember
  | ClassConstructor
  | ClassMethod
  | ClassOperator {extends=Statement}

Operator ::= '+' | '-' | '*' | '/' | '^' | '!' | '==' | '!='
  | '++' | '--' | '&&' | '||' | '>=' | '<=' | '>' | '<'
  | '&' | '|' | '~' | '>>' | '<<' | '[' ']'

ClassConstructor ::= FunctionName Arguments ConstructorArguments? CodeBlock
ConstructorArguments ::= ':' ('super' | 'this') CallArguments
ClassOperator ::= 'operator' Operator OperatorArguments NamedTypeHint? CodeBlock { methods=[operator="OPERATOR[0]"] }
ClassMethod ::= FunctionModifiers 'fun' FunctionName Arguments NamedTypeHint? CodeBlock
ClassMember ::= FunctionModifiers 'var' VariableName NamedTypeHint? ['=' Expression] ';'
FunctionModifiers ::= ( 'static'? ) {
  mixin="me.hardcoded.arucas.psi.impl.ArucasModifierElementImpl"
  implements="me.hardcoded.arucas.psi.ArucasModifierElement"
  methods=[isStatic]
}

NamedTypeHint ::= ':' NamedTypeHintName ( '|' NamedTypeHintName )*
NamedTypeHintName ::= ClassName

Arguments ::= '(' IDENTIFIER ArgumentArbitrary ')' 
    | '(' [ Argument (',' Argument)* ] ')'
ArgumentArbitrary ::= '...'
Argument ::= IdentifierName NamedTypeHint?

OperatorArguments ::= Arguments

/* Function Statement */
FunctionStatement ::= 'fun' FunctionName Arguments NamedTypeHint? CodeBlock {extends=Statement}
TryStatement ::= 'try' Statement 'catch' '(' IdentifierName NamedTypeHint? ')' Statement TryStatementFinally? {
  extends=Statement
  methods=[
    TRY_BODY="Statement[0]"
    CATCH_BODY="Statement[1]"
  ]
}
TryStatementFinally ::= 'finally' Statement

/* For Statements */
ForEachStatement ::= 'foreach' '(' IdentifierName ':' Expression ')' Statement {
  extends=Statement
}
ForStatement ::= 'for' '(' Expression? ';' Expression? ';' Expression? ')' Statement {extends=Statement}

/* Switch Statement */
SwitchStatement ::= 'switch' '(' Expression ')' SwitchCodeBlock {extends=Statement}
SwitchCodeBlock ::= '{' CaseStatement* '}' {extends=Statement}
CaseStatement ::= ('default' | 'case' CaseValues) '->' Statement

CaseValues ::= CaseValue (',' CaseValue)*
CaseValue ::= CaseString | CaseNumber
private CaseString ::= STRING
private CaseNumber ::= ['-'] NUMBER

/* Expressions */
Expression ::= AssignExpression
  | ConditionalOrExpression
  | ConditionalAndExpression
  | UnaryNotExpression
  | ArrayExpression
  | RelationalExpression
  | ArithmeticExpression
  | BitExpression
  | FactorExpression
  | UnaryExpression
  | PowerExpression
  | CallExpression
  | MemberExpression
  | AtomExpressionGroup

AssignExpression ::= IdentifierName ('++' | '--' | '=' Expression) {extends=Expression}
ConditionalOrExpression ::= Expression '||' Expression {extends=Expression}
ConditionalAndExpression ::= Expression '&&' Expression {extends=Expression}
RelationalExpression ::= Expression ('==' | '!=' | '<' | '>' | '<=' | '>=') Expression {extends=Expression}
ArithmeticExpression ::= Expression ('+' | '-') Expression {extends=Expression}
BitExpression ::= Expression ('>>' | '<<' | '~' | '|' | '&') Expression {extends=Expression}
FactorExpression ::= Expression ('*' | '/') Expression {extends=Expression}
ArrayExpression ::= Expression '[' Expression ']' ( '=' Expression )? {extends=Expression}
UnaryNotExpression ::= '!' Expression {extends=Expression}
UnaryExpression ::= ('+' | '-') Expression {extends=Expression}
PowerExpression ::= Expression '^' Expression {extends=Expression}
CallExpression ::= Expression &'(' CallArguments {extends=Expression}
CallArguments ::= '(' [ Expression (',' Expression)* ] ')' {pin=1}
MemberExpression ::= Expression '.' ( &IDENTIFIER ) Expression {
  extends=Expression
  methods=[
    LEFT="Expression[0]"
    RIGHT="Expression[1]"
  ]
}
private AtomExpressionGroup ::= AtomExpression
  | &'[' ListExpression
  | &'{' MapExpression
  | &'fun' LambdaExpression
  | &'new' NewExpression
  | &'(' ParenthesesExpression

AtomExpression ::= IdentifierName
  | ValueKeyword
  | STRING
  | NUMBER {
  extends=Expression
  mixin="me.hardcoded.arucas.psi.impl.ArucasAtomElementImpl"
  implements="me.hardcoded.arucas.psi.ArucasAtomElement"
  methods=[getAtomType]
}
private ValueKeyword ::= KW_TRUE | KW_FALSE | KW_NULL | KW_THIS

ListExpression ::= '[' ListElements? ']' {extends=Expression pin=1}
private ListElements ::= Expression (',' Expression)*
MapExpression ::= '{' MapElements? '}' {extends=Expression pin=1}
private MapElements ::= MapEntry (',' MapEntry)*
MapEntry ::= Expression ':' Expression {
  methods=[
    key="Expression[0]"
    value="Expression[1]"
  ]
}
LambdaExpression ::= 'fun' Arguments NamedTypeHint? ( CodeBlock | Expression ) {extends=Expression}
NewExpression ::= 'new' ClassName CallArguments {extends=Expression}
ParenthesesExpression ::= '(' Expression ')' {extends=Expression}
